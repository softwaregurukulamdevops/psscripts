terraform {
  required_providers {
    azurerm = {
      source  = "hashicorp/azurerm"
      version = ">= 3.0.0"
    }
    random = {
      source  = "hashicorp/random"
      version = ">= 3.0.0"
    }
  }

  required_version = ">= 1.3.0"
}

provider "azurerm" {
  features {}
  subscription_id = "3bc8f069-65c7-4d08-b8de-534c20e56c38"
}

provider "random" {}

# Use full Azure resource ID format for management group ID here
variable "management_group_id" {
  type    = string
  default = "/providers/Microsoft.Management/managementGroups/Managementgrouptest"
}

# Create a Resource Group
resource "azurerm_resource_group" "network_rg" {
  name     = "rg-network-monitoring"
  location = "eastus"
}

# Generate random suffix for globally unique names
resource "random_string" "suffix" {
  length  = 6
  upper   = false
  special = false
}

# Create a Storage Account
resource "azurerm_storage_account" "flow_logs_storage" {
  name                     = "flowlogsstorage${random_string.suffix.result}"
  resource_group_name      = azurerm_resource_group.network_rg.name
  location                 = azurerm_resource_group.network_rg.location
  account_tier             = "Standard"
  account_replication_type = "LRS"
  tags = {
    environment = "networking"
  }
}

# Create a Log Analytics Workspace
resource "azurerm_log_analytics_workspace" "traffic_analytics" {
  name                = "workspace-network-${random_string.suffix.result}"
  location            = azurerm_resource_group.network_rg.location
  resource_group_name = azurerm_resource_group.network_rg.name
  sku                 = "PerGB2018"
  retention_in_days   = 30
}

# Policy Assignment at Management Group Level
resource "azurerm_management_group_policy_assignment" "vnet_flow_log_analytics" {
  name                 = "vnet-flow-log"
  display_name         = "Configure VNet to enable Flow Log and Traffic Analytics"
  policy_definition_id = "/providers/Microsoft.Authorization/policyDefinitions/3e9965dc-cc13-47ca-8259-a4252fd0cf7b"
  management_group_id  = var.management_group_id
  description          = "Ensure virtual networks have flow logs and traffic analytics enabled."
identity {
    type = "SystemAssigned"
  }
  location = "eastus"
  parameters = jsonencode({
    "vnetRegion" = {
      "value" = azurerm_resource_group.network_rg.location
    },
    "storageId" = {
      "value" = azurerm_storage_account.flow_logs_storage.id
    },
    "workspaceResourceId" = {
      "value" = azurerm_log_analytics_workspace.traffic_analytics.id
    },
    "workspaceRegion" = {
      "value" = azurerm_resource_group.network_rg.location
    },
    "networkWatcherName" = {
      "value" = "NetworkWatcher_${azurerm_resource_group.network_rg.location}"
    }
  })
}
